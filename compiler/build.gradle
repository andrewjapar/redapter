apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
    implementation project(':annotations')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "com.squareup:kotlinpoet:$kotlin_poet"

    implementation "com.google.auto.service:auto-service:$auto_service"
    kapt "com.google.auto.service:auto-service:$auto_service"
}

group = 'com.andrewjapar.redapter'
version = '0.1.0-alpha1'

sourceCompatibility = "8"
targetCompatibility = "8"

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

tasks.register("javadocJar", Jar) {
    from javadoc
    classifier "javadoc"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'redapter-compiler'
            groupId this.group
            version this.version
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "Redapter-Compiler"
                description = "Redapter is a lazy tool for Android to compose your Adapter and bind with any ViewHolder by adding Annotations."
                url = "https://github.com/andrewjapar/redapter"
                licenses {
                    license {
                        name = "MIT"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                developers {
                    developer {
                        id = "andrewjapar"
                        name = "Andrew Japar"
                        email = "andrewjap.dev@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/andrewjapar/redapter.git"
                    developerConnection = "scm:git:https://github.com/andrewjapar/redapter.git"
                    url = "https://github.com/andrewjapar/redapter"
                }
            }
        }
    }
}

bintray {
    user = project.getProperty("BINTRAY_USER")
    key = project.getProperty("BINTRAY_KEY")
    publications = ['mavenJava']
    pkg {
        repo = 'Android'
        name = 'Redapter-Compiler'

        vcsUrl = 'https://github.com/andrewjapar/redapter.git'
        licenses = ['MIT']
    }
}